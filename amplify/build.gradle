buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.12.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
        classpath 'com.noveogroup.android:check:1.2.3'
        classpath 'com.btkelly:gnag:0.1.4'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.4.0x'
    }
}

plugins {
    id "com.github.hierynomus.license" version "0.12.1"
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'gnag-plugin'
apply plugin: 'com.noveogroup.android.check'
apply from: 'coverage.gradle'

// Shared project metadata

ext {
    amplifyVersion = "1.3.0"
    amplifyWebsiteUrl = 'https://github.com/stkent/amplify'
    amplifyGitHubRepoName = 'stkent/bugshaker-android'
    amplifyVcsUrl = "https://github.com/${amplifyGitHubRepoName}.git"
    amplifyGroupId = 'com.github.stkent'
    amplifyArtifactId = 'amplify'
    amplifyDescription = "Respectfully request feedback in your Android app."
}

license {
    header = file('../Apache2License.txt')
    strictCheck true
}

configurations {
    javadocDeps
}

// If this is skipped, the upload to Bintray will succeed but the version in filenames will be
// 'unspecified'.
version = amplifyVersion

// If this is skipped, the upload to Bintray will succeed but the reported group id will be
// the root project name (in this case, amplify-android) rather than the group id specified
// in the install or bintray configuration closures.
group = amplifyGroupId

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName amplifyVersion
    }

    lintOptions {
        abortOnError false
    }

    testOptions.unitTests {
        returnDefaultValues = true
        all {
            maxParallelForks = 2
            forkEvery = 150
            afterTest { descriptor, result ->
                println "Executing test for ${descriptor.name} with result: ${result.resultType}"
            }
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
    }
}

gnag {
    gitHubRepoName 'stkent/amplify'
    failBuildOnError true
}

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.0.44-beta'
    javadocDeps 'com.android.support:support-annotations:23.1.1'
    compile 'com.android.support:appcompat-v7:23.1.1'
}

check {
    abortOnError false
    checkstyle {
        config project.file('config/checkstyle.xml')
    }

    findbugs {
        config hard()
    }

    pmd {
        config project.file('config/pmd.xml')
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            groupId amplifyGroupId
            artifactId amplifyArtifactId
            version amplifyVersion
            packaging 'aar'

            // This does not seem to delegate to project.name as I expected it would.
            name amplifyArtifactId
            description amplifyDescription
            inceptionYear '2015'
            url amplifyWebsiteUrl

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }

            developers {
                developer {
                    id 'stkent'
                    name 'Stuart Kent'
                    email 'skent.dev@gmail.com'
                }
            }

            scm {
                connection amplifyVcsUrl
                url amplifyWebsiteUrl
            }
        }
    }
}

ext {
    // command-line deployment
    bintrayUserParameterName = 'bintrayUser'
    bintrayKeyParameterName = 'bintrayKey'

    // CI deployment
    bintrayUserEnvVarName = 'BINTRAY_USER'
    bintrayKeyEnvVarName = 'BINTRAY_KEY'
}

bintray {
    user = project.hasProperty((String) bintrayUserParameterName) ?
            project.property((String) bintrayUserParameterName) :
            System.getenv((String) bintrayUserEnvVarName)

    key = project.hasProperty((String) bintrayKeyParameterName) ?
            project.property((String) bintrayKeyParameterName) :
            System.getenv((String) bintrayKeyEnvVarName)

    dryRun = false
    publish = true

    configurations = ['archives']

    pkg {
        repo = 'android-libraries'
        name = 'amplify'
        desc = amplifyDescription
        websiteUrl = amplifyWebsiteUrl
        labels = ['android', 'ratings', 'feedback']
        licenses = ['Apache-2.0']
        publicDownloadNumbers = true

        vcsUrl = amplifyVcsUrl
        githubRepo = amplifyGitHubRepoName
        issueTrackerUrl = "https://github.com/${amplifyGitHubRepoName}/issues"

        version {
            name = amplifyVersion
            released = new Date()
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += configurations.javadocDeps
//    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.getDestinationDir()
    classifier = 'javadoc'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
