buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        jcenter()
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.14.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
        classpath "se.bjurr.violations:violation-comments-to-github-gradle-plugin:1.31"
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.5.0-x'
    }
}

plugins {
    id "com.github.hierynomus.license" version "0.13.1"
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'checkstyle'
apply plugin: "se.bjurr.violations.violation-comments-to-github-gradle-plugin"

ext {
    // Library info:
    libraryVersion = '2.1.1'
    libraryName = 'amplify'
    libraryGroupId = 'com.github.stkent'
    libraryArtifactId = 'amplify'
    libraryDescription = 'Respectfully request feedback in your Android app.'
    libraryInceptionYear = '2015'
    libraryGitHubRepoName = 'stkent/amplify'
    libraryGitHubUrl = "https://github.com/${libraryGitHubRepoName}"
    libraryIssueTrackerUrl = "${libraryGitHubUrl}/issues"
    libraryWebsiteUrl = libraryGitHubUrl
    libraryLabels = ['android', 'ratings', 'feedback']
    libraryLicenseName = 'Apache License Version 2.0'
    libraryLicenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.html'

    // Developer info:
    developerId = 'stkent'
    developerName = 'Stuart Kent'
    developerEmail = 'skent.dev@gmail.com'

    // Bintray categorization information:
    bintrayRepo = 'android-libraries'
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 1
        versionName libraryVersion
    }

    lintOptions {
        abortOnError false
    }

    testOptions.unitTests {
        returnDefaultValues = true
        all {
            maxParallelForks = 2
            forkEvery = 150
            afterTest { descriptor, result ->
                println "Executing test for ${descriptor.name} with result: ${result.resultType}"
            }
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
    }
}

task checkstyle(type: Checkstyle) {
    description 'Check code standard'
    group 'verification'

    configFile file('./config/checkstyle.xml')
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    classpath = files()
    ignoreFailures = true
}

task violationCommentsToGitHub(type: se.bjurr.violations.comments.github.plugin.gradle.ViolationCommentsToGitHubTask) {
    repositoryOwner = "stkent";
    repositoryName = "amplify"
    pullRequestId = System.properties['issueNumber']
    oAuth2Token = System.properties['authToken']
    gitHubUrl = "https://api.github.com/"
    createCommentWithAllSingleFileComments = false
    createSingleFileComments = true
    commentOnlyChangedContent = false
    minSeverity = se.bjurr.violations.lib.model.SEVERITY.INFO //ERROR, INFO, WARN
    violations = [
            ["FINDBUGS",   ".", ".*/findbugs/.*\\.xml\$",   "Findbugs"],
            ["PMD",        ".", ".*/pmd/.*\\.xml\$",        "PMD"],
            ["CHECKSTYLE", ".", ".*/checkstyle/.*\\.xml\$", "Checkstyle"]
    ]
}

//gnag {
//    failOnError true
//
//    github {
//        repoName libraryGitHubRepoName
//    }
//
//    checkstyle {
//        reporterConfig project.file('config/checkstyle.xml')
//    }
//
//    pmd {
//        enabled true
//        reporterConfig project.file('config/pmd.xml')
//    }
//
//    findbugs {
//        enabled true
//        reporterConfig project.file('config/findbugs.xml')
//    }
//
//    androidLint {
//        enabled true
//        severity 'Error'
//    }
//}

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.7.14'
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
}

apply from: 'https://raw.githubusercontent.com/stkent/JCenter/master/bintray1.7.3.gradle'
apply from: 'https://raw.githubusercontent.com/stkent/JCenter/master/install1.5.gradle'
apply from: 'https://raw.githubusercontent.com/stkent/JCenter/master/license0.13.1.gradle'
apply from: 'coverage.gradle'
